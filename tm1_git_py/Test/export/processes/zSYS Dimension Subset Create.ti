#region Prolog

#****Begin: Generated Statements***
#****End: Generated Statements****

# =========================================================
# Original Date Created              2015/02/03
# Originator                                 Moravcsik Zsolt
# Purpose                                   General Process to Create Subsets. If the is a element or element separated by "|" the subset will contain that element(s) to the subset
#                                                 if it is an C element and the pExpandAndChangeC toN is 1 it will be contain all of the N level child of that element
#                                                 if it is an C element and the pExpandAndChangeCtoN is 0 it will be contain the exact C element only
#                                                 if it is '' then it will be all n level element
#                                                 it is a subset it will be copied the exact content of this subset, if it is an MDX the subset will be defined by that.
#                                                Please use a unique number in the subset names  which has been generated by the caller process by this Round ( RAND * 1000000). This is for unique run time information (beware of paralel collisons)
#                                                Please use this process calling every time: ExecuteProcess('zSYS Dimension Subset Create','pDimName',pDimName,'pSubName',pSubName,'pStringParam',pStringParam,'pExpandAndChangeCtoN',1);
# Change Date                           
# Change Request Reference    
# Change Made by                     
# =========================================================

# Central logging - start
nRandomNumber = Round ( RAND * 1000000);
sRandomNumber = NumberToString(nRandomNumber);
pStartTime=NumberToString(NOW());
pProcessName =  GetProcessName();
pParams= 'pDimName|'|pDimName|'|pSubName|'|pSubName|'|pStringParam|'|pStringParam|'|pExpandAndChangeCtoN|'|NumberToString(pExpandAndChangeCtoN)|'|pDebug|'|NumberToString(pDebug);
#ExecuteProcess ('zSYS Process Log Create Entry','pProcessName',pProcessName,'pStartTime',pStartTime,'pParams',pParams,'pRandomNumber',sRandomNumber,'pCallerID',pCallerID);

# Define variables
vsProcessName =  GetProcessName();
vsTimeStamp = TIMST(NOW(),'\Y\m\d\h\i\s');
vsDelimiter = pDelimiter;
vsDelimiter = IF(vsDelimiter @='','|',vsDelimiter );

# Check if pDimName exists
If (DimensionExists(pDimName)=0);
ProcessBreak;
EndIf;


# Check if pStringParam exists in the dimension we put into the subset
IF(DIMIX(pDimname,pStringParam)>0);

    # Else check if pStringParam exists and n lvl element in the dimension we put into the subset
   IF(ELLEV( pDimName, pStringParam ) = 0);
      IF(pDebug = 1);
           LogoutPut('INFO',pStringParam | ' inserted into '| pSubname |' subset of'| pDimName| ' dimension.');
      ELSE;
          SubsetCreate(pDimName,pSubName);
          SubsetElementInsert(pDimName,pSubName,pStringParam, 1);
      ENDIF;
    # Else check if pStringParam exists and C lvl element and the param pExpandAndChangeCtoN= 0 we put into the subset
   ELSEIF(ElLev( pDimName, pStringParam ) <> 0 & pExpandAndChangeCtoN=0 );
      IF(pDebug = 1);
         LogoutPut('INFO',pStringParam | ' inserted into '| pSubname |' subset of'| pDimName| ' dimension.');
      ELSE;
         SubsetCreate(pDimName,pSubName);
         SubsetElementInsert(pDimName,pSubName,pStringParam, 1);
      ENDIF;
    # Else check if pStringParam exists and C lvl element and the param pExpandAndChangeCtoN= 0 we expand the C element and put into the subset
   ELSEIF(ELLEV( pDimName, pStringParam ) <> 0 & pExpandAndChangeCtoN<>0 );
      vsMDX = '{TM1FILTERBYLEVEL( {TM1DRILLDOWNMEMBER( {[' | pDimname | '].[' | pStringParam | ']}, ALL, RECURSIVE )}, 0)}';
      IF(pDebug = 1);
         LogoutPut('INFO',vsMDX | 'subset created by this MDX.');
      ELSE;
         SubsetCreateByMDX( pSubName, vsMDX );
      ENDIF;
   ENDIF;

# Else Check if pStringParam is a subset copy all element from the subet to our subset
ELSEIF(SubsetExists(pDimname,pStringParam)=1);

   vnMax = SubsetGetSize(pDimname,pStringParam);
    i =1;
   SubsetCreate(pDimName,pSubName);

   WHILE(i <= vnMax);
      IF(pDebug = 1);
         LogoutPut('INFO',SubsetGetElementName( pDimname, pStringParam, i ) | ' inserted into '| pSubname |' subset of'| pDimName| ' dimension.');
      ELSE;
         SubsetElementInsert( pDimName, pSubName, SubsetGetElementName( pDimname, pStringParam, i ), 1 );
      ENDIF;
       i= i +1;
   END;
# Else check if pEl consists of numerous elements concatenated by the given System Parameter
ELSEIF (SCAN(vsDelimiter,pStringParam)>0);

   vsElementName = '';
   sMDX ='{'; 
   WHILE(SCAN(vsDelimiter,pStringParam) >0);
       i = SCAN (vsDelimiter,pStringParam) ;
      vsElementName = SUBST(pStringParam,1,i-1);
      pStringParam = SUBST(pStringParam,i+1,LONG(pStringParam)-i);
      IF (DIMIX(pDimName,vsElementname)>0);
           sMDX = sMDX|'['|pDimName|'].['|vsElementName|'],';
      ENDIF;
        IF( pDebug = 1);
                  LogoutPut('INFO',pStringParam| ' ' |vsELementName| ' ' |sMDX | ' the base set cretion of MDX in progress.');
         EndIF;
   END;

   IF (DIMIX(pDimName,pStringParam)>0);

           sMDX = sMDX|'['|pDimName|'].['|pStringParam|']}';  
   ENDIF;

   IF( pExpandAndChangeCtoN=0);
         IF( pDebug = 1);
                  LogoutPut('INFO',sMDX | 'subset created by this MDX.');
         Else;
                  SubsetCreateByMDX(pSubName,sMDX);
         EndIF;
   ELSEIF( pExpandAndChangeCtoN=1);
         sMDX='{TM1FILTERBYLEVEL( {TM1DRILLDOWNMEMBER( '| sMDX|' , ALL, RECURSIVE )}, 0)}';
         IF( pDebug = 1);
                  LogoutPut('INFO',sMDX | 'subset created by this MDX.');
         Else;
                  SubsetCreateByMDX(pSubName,sMDX);       
         EndIF;
   ENDIF;

# Elseif the pStringParam is align with this: start '{Â´ and finished '}' and contain the '[pDimName]' substring we try to create subset by mdx
ELSEIF ((Scan('['|pDimname|']', pStringParam) > 0) & (Subst(pStringParam,1,1) @= '{') & (Subst(pStringParam,Long(pStringParam),1) @= '}') );

   vsMDX= pStringParam;
   IF(pDebug = 1);
      LogoutPut('INFO',vsMDX | 'subset created by this MDX.');
   ELSE;
      SubsetCreateByMDX( pSubName, vsMDX );
   ENDIF;

# Elseif the pStringParam is empty we assign a n element dynamic subset
ELSEIF (pStringParam @='');
   vsMDX='{TM1FILTERBYLEVEL( {TM1SUBSETALL( ['| pDimName |'] )}, 0)}'; 
   IF(pDebug = 1);
      LogoutPut('INFO',vsMDX | 'subset created by this MDX.');
   ELSE;
      SubsetCreateByMDX( pSubName, vsMDX );
   ENDIF;

ELSE;
   IF(pDebug = 1);
      LogoutPut('INFO','This was the exectue parameters:'| pDimname| ' dimension' | pSubName| ' subset' |pStringParam | ' with this string parameters.');
   ELSE;     
      ItemReject(pStringParam|' is not an valid parameter');
      ProcessBreak;
   ENDIF;
ENDIF;

#endregion
#region Metadata

#****Begin: Generated Statements***
#****End: Generated Statements****
#endregion
#region Data

#****Begin: Generated Statements***
#****End: Generated Statements****
#endregion
#region Epilog

#****Begin: Generated Statements***
#****End: Generated Statements****

# Central logging - finish
#ExecuteProcess('zSYS Process Log Close Entry','pProcessName',pProcessName,'pStartTime',pStartTime,'pRandomNumber',sRandomNumber,'pProcessErrorLogFile',GetProcessErrorFilename);

#endregion
