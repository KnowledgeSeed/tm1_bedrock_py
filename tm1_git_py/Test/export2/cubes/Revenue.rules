SKIPCHECK;


#Botond 06.07
#Felesleges főkönyvek kihagyása
['Ledger':'Ledger':'II. Koltseg'] = STET;


['Ledger':'Ledger':'EBITDA'] = STET;

# ARPU C-s szab
['Ledger':'Ledger':'Nem tartozik fokonyhoz','Revenue Lineitem':'ARPU','Revenue Measure':'Value'] = C:
IF( ELLEV('Period',!Period)>0,
  DB('Revenue', !Version, DB('}ElementAttributes_Period', !Period, 'Last_M_Y'), 'Nem tartozik fokonyhoz', !Product, 'ARPU', 'Value'),
STET);

# EFI C-s szabály
# MODEM
['Ledger':'Ledger':'91502','Revenue Lineitem':'Efi szam','Revenue Measure':'Value'] = C:
IF( ELLEV('Period',!Period)>0,
  DB('Revenue', !Version, DB('}ElementAttributes_Period', !Period, 'Last_M_Y'), '91502', 'Nem tartozik termekhez', 'Efi szam', 'Value'),
STET);


# EFI C-s szab
# STB
['Ledger':'Ledger':'91501','Revenue Lineitem':'Efi szam','Revenue Measure':'Value'] = C:
IF( ELLEV('Period',!Period)>0,
  DB('Revenue', !Version, DB('}ElementAttributes_Period', !Period, 'Last_M_Y'), '91501', !Product, 'Efi szam', 'Value'),
STET);

# ARPU C-s szab
# MODEM
['Ledger':'Ledger':'91502','Revenue Lineitem':'ARPU','Revenue Measure':'Value'] = C:
IF( ELLEV('Period',!Period)>0,
  DB('Revenue', !Version, DB('}ElementAttributes_Period', !Period, 'Last_M_Y'), '91502', 'Nem tartozik termekhez', 'ARPU', 'Value'),
STET);


# ARPU C-s szab
# csak a STB
['Ledger':'Ledger':'91501','Revenue Lineitem':'ARPU','Revenue Measure':'Value'] = C:
IF( ELLEV('Period',!Period)>0,
  DB('Revenue', !Version, DB('}ElementAttributes_Period', !Period, 'Last_M_Y'), '91501', !Product, 'ARPU', 'Value'),
STET);


# C-s szab
# csak az eszközértékesítésnél
['Ledger':'Ledger':'91401','Product':'Nem tartozik termekhez','Revenue Lineitem':'Koltseg Rata','Revenue Measure':'Value'] = C:
IF( ELLEV('Period',!Period)>0,
  DB('Revenue', !Version, DB('}ElementAttributes_Period', !Period, 'Last_M_Y'), '91401', 'Nem tartozik termekhez', 'Koltseg Rata', 'Value'),
STET);

#TITLE: 
#VIEWS:
#LINKED:
# bevétel: ténynél DWH, tervnél:ARPU X darab
['ledger':'ledger':{'91501'},'Revenue Lineitem':'Revenue Lineitem':'Bevetel',
'Product':'Total Products', 'Revenue Measure':'Revenue Measure':'Value'] = C:
IF( DIMIX('Period',!Period) >= DIMIX('Period',DB('zSYS Maintenance Parameter','ActualMonth','S Type')),
['Revenue Lineitem':'Revenue Lineitem':'Efi szam']
*
['Revenue Lineitem':'Revenue Lineitem':'ARPU'],
STET
);

# verzo szabályok
[]= N:
IF(ELISPAR('Version','Report',!Version) = 1,
STET,Continue);



###########################################################
# EGYEDI FŐKÖNYV SZABÁLYOK:
# E/1: eszközértékesítés
# E/2: modem szabályok
# E/3: dekóder STB

# Sima inputok (egyéb bev)
# kalkulációs szabályok (készülék eladás bevételek)
###########################################################

# E/1: eszközértékesítés
#Tényidőszakban, tervben
#darabszám: connect, input
#fajlagos(ARPU) kalkulált, input
#bevétel: DWH(főkönyv), kalkuláció
#költség%: kalkulált, input
#költség: DWH(főkönyv), kalkuláció
['ledger':'Ledger':{'91401'},'Revenue Lineitem':'Revenue Lineitem':'Efi szam',
 'Product':'Nem tartozik termekhez', 'Revenue Measure':'Revenue Measure':'Value'] = N:
STET
;

# ARPU
['ledger':'ledger':{'91401'},'Revenue Lineitem':'Revenue Lineitem':'ARPU',
 'Product':'Nem tartozik termekhez', 'Revenue Measure':'Revenue Measure':'Value'] = N:
IF( DIMIX('Period',!Period) >= DIMIX('Period',DB('zSYS Maintenance Parameter','ActualMonth','S Type')),
STET,
['Revenue Lineitem':'Revenue Lineitem':'Bevetel']
\
['Revenue Lineitem':'Revenue Lineitem':'Efi szam']
);

#bevétel: DWH(főkönyv), kalkuláció
['ledger':'ledger':{'91401'},'Revenue Lineitem':'Revenue Lineitem':'Bevetel',
 'Product':'Nem tartozik termekhez', 'Revenue Measure':'Revenue Measure':'Value'] = N:
IF( DIMIX('Period',!Period) >= DIMIX('Period',DB('zSYS Maintenance Parameter','ActualMonth','S Type')),
['Revenue Lineitem':'Revenue Lineitem':'Efi szam']
*
['Revenue Lineitem':'Revenue Lineitem':'ARPU']
,
STET
);

#költség%: kalkulált, input
['ledger':'ledger':{'91401'},'Revenue Lineitem':'Revenue Lineitem':'Koltseg Rata',
 'Product':'Nem tartozik termekhez', 'Revenue Measure':'Revenue Measure':'Value'] = N:
IF( DIMIX('Period',!Period) >= DIMIX('Period',DB('zSYS Maintenance Parameter','ActualMonth','S Type')),
STET,
['Revenue Lineitem':'Revenue Lineitem':'Koltseg']
\
['Revenue Lineitem':'Revenue Lineitem':'Bevetel']
);

#költség: DWH(főkönyv), kalkuláció
['ledger':'ledger':{'91401'},'Revenue Lineitem':'Revenue Lineitem':'Koltseg',
 'Product':'Nem tartozik termekhez', 'Revenue Measure':'Revenue Measure':'Value'] = N:
IF( DIMIX('Period',!Period) >= DIMIX('Period',DB('zSYS Maintenance Parameter','ActualMonth','S Type')),
['Revenue Lineitem':'Revenue Lineitem':'Bevetel']
*
['Revenue Lineitem':'Revenue Lineitem':'Koltseg Rata'],
STET
);

# E/2:modem szabályok
# darab (TM1) tény és terv időszakba is a naturáliából
['ledger':'ledger':{'91502'},'Revenue Lineitem':'Revenue Lineitem':'Efi szam','Revenue Measure':'Revenue Measure':'Value','Product':'Nem tartozik termekhez'] = N:
  0.5
  *
  ( DB('Subscription Report', !Version, !Period, 'Total', 'Total', '543', 'Value')
  +
  DB('Subscription Report', !Version, DB('}ElementAttributes_Period', !Period, 'PREV_1M'), 'Total', 'Total', '543', 'Value')
  )
  *
  DB('Product Ledger Assignment', !Version, !Period, !ledger, '543', 'Value');

# ARPU: tervben input, tény időszakban kalkuláció (főkönyv (DWH) / darab (TM1) )
['ledger':'ledger':{'91502'},'Revenue Lineitem':'Revenue Lineitem':'ARPU','Revenue Measure':'Revenue Measure':'Value','Product':'Nem tartozik termekhez'] = N:
IF( DIMIX('Period',!Period) >= DIMIX('Period',DB('zSYS Maintenance Parameter','ActualMonth','S Type')),
STET,
['Revenue Lineitem':'Revenue Lineitem':'Bevetel']
\
['Revenue Lineitem':'Revenue Lineitem':'Efi szam']
);

#TITLE: 
#VIEWS:
#LINKED:
# bevétel: ténynél DWH, tervnél:ARPU X darab
['ledger':'ledger':{'91502'},'Revenue Lineitem':'Revenue Lineitem':'Bevetel','Revenue Measure':'Revenue Measure':'Value','Product':'Nem tartozik termekhez'] = N:
IF( DIMIX('Period',!Period) >= DIMIX('Period',DB('zSYS Maintenance Parameter','ActualMonth','S Type')),
['Revenue Lineitem':'Revenue Lineitem':'Efi szam']
*
['Revenue Lineitem':'Revenue Lineitem':'ARPU'],
STET
);

# E/3: dekóder STB
# darab (TM1) tény és terv időszakba is a naturáliából
#199 - 203
['ledger':'ledger':{'91501'} ,'Revenue Lineitem':'Revenue Lineitem':'Efi szam',
'Product':'Nem tartozik termekhez', 'Revenue Measure':'Revenue Measure':'Value'] = N:
  0.5
  *
  (
  DB('Subscription Report', !Version, !Period, 'Total', 'Total', '199', 'Value') +
  DB('Subscription Report', !Version, !Period, 'Total', 'Total', '200', 'Value') +
  DB('Subscription Report', !Version, !Period, 'Total', 'Total', '201', 'Value') +
  DB('Subscription Report', !Version, !Period, 'Total', 'Total', '202', 'Value') +
  DB('Subscription Report', !Version, !Period, 'Total', 'Total', '203', 'Value') +
  DB('Subscription Report', !Version, DB('}ElementAttributes_Period', !Period, 'PREV_1M'), 'Total', 'Total', '199', 'Value')+
  DB('Subscription Report', !Version, DB('}ElementAttributes_Period', !Period, 'PREV_1M'), 'Total', 'Total', '200', 'Value')+
  DB('Subscription Report', !Version, DB('}ElementAttributes_Period', !Period, 'PREV_1M'), 'Total', 'Total', '201', 'Value')+
  DB('Subscription Report', !Version, DB('}ElementAttributes_Period', !Period, 'PREV_1M'), 'Total', 'Total', '202', 'Value')+
  DB('Subscription Report', !Version, DB('}ElementAttributes_Period', !Period, 'PREV_1M'), 'Total', 'Total', '203', 'Value')
  )
;

#TITLE: 
#VIEWS:
#LINKED:
# ARPU: tervben input, tény időszakban kalkuláció (főkönyv (DWH) / darab (TM1) )
['ledger':'ledger':{'91501'},'Revenue Lineitem':'Revenue Lineitem':'ARPU',
'Product':'Nem tartozik termekhez', 'Revenue Measure':'Revenue Measure':'Value'] = N:
IF( DIMIX('Period',!Period) >= DIMIX('Period',DB('zSYS Maintenance Parameter','ActualMonth','S Type')),
STET,
['Revenue Lineitem':'Revenue Lineitem':'Bevetel']
\
['Revenue Lineitem':'Revenue Lineitem':'Efi szam']
);

# bevétel: ténynél DWH, tervnél:ARPU X darab
['ledger':'ledger':{'91501'},'Revenue Lineitem':'Revenue Lineitem':'Bevetel',
'Product':'Nem tartozik termekhez', 'Revenue Measure':'Revenue Measure':'Value'] = N:
IF( DIMIX('Period',!Period) >= DIMIX('Period',DB('zSYS Maintenance Parameter','ActualMonth','S Type')),
['Revenue Lineitem':'Revenue Lineitem':'Efi szam']
*
['Revenue Lineitem':'Revenue Lineitem':'ARPU'],
STET
);

##TITLE: 
##VIEWS:
##LINKED:
## bevétel: ténynél DWH, tervnél:ARPU X darab
#['ledger':'ledger':{'91501'},'Revenue Lineitem':'Revenue Lineitem':'Bevetel',
#'Product':'Total Products', 'Revenue Measure':'Revenue Measure':'Value'] = C:
#IF( DIMIX('Period',!Period) >= DIMIX('Period',DB('zSYS Maintenance Parameter','ActualMonth','S Type')),
#['Revenue Lineitem':'Revenue Lineitem':'Efi szam']
#*
#['Revenue Lineitem':'Revenue Lineitem':'ARPU'],
#STET
#);


## ideiglenesen kivéve 06 24
#['Revenue Lineitem':'Revenue Lineitem':'ARPU','ledger':'ledger':{'91101','91102','91201','91202','91301','91302','91303','91304'},
#'Revenue Measure':'Revenue Measure':'Value'] = N:
#['ledger':'ledger':'Nem tartozik fokonyhoz'];



# Sima inputok tervidőszakban:
# csak bevétel sor van: minden stet
# bevételnél a tény mindenhol stet
['Revenue Lineitem':'Revenue Lineitem':'Bevetel','Revenue Measure':'Revenue Measure':'Value'] = N:
IF( DIMIX('Period',!Period) < DIMIX('Period',DB('zSYS Maintenance Parameter','ActualMonth','S Type')),
STET,
Continue
);

#TITLE: 
#VIEWS:
#LINKED:
############################################
# ÁLTALÁNOS SZABÁLYOK
# a, az összerendelések alapján a záróértékek áthúzása a report kockából
['Revenue Lineitem':'Revenue Lineitem':'Efi szam','Revenue Measure':'Revenue Measure':'Value'] = N:
  0.5
  *
  ( DB('Subscription Report', !Version, !Period, 'Total', 'Total', !Product, 'Value')
  +
  DB('Subscription Report', !Version, DB('}ElementAttributes_Period', !Period, 'PREV_1M'), 'Total', 'Total', !Product, 'Value')
  )
  *
  DB('Product Ledger Assignment', !Version, !Period, !ledger, !Product, 'Value');

#TITLE: 
#VIEWS:
#LINKED:
# b, ARPU mindenhol input
['Revenue Lineitem':'Revenue Lineitem':'ARPU','Revenue Measure':'Revenue Measure':'Value'] = N:
STET;

['Revenue Lineitem':'Revenue Lineitem':'Bevetel','Revenue Measure':'Revenue Measure':'Value'] = N:
IF( DB('}ElementAttributes_ledger', !ledger, 'Efi szam kalkulacio') @= 'from naturalia',
['Revenue Lineitem':'Revenue Lineitem':'Efi szam']
*
#['Revenue Lineitem':'Revenue Lineitem':'ARPU','ledger':'ledger':'Nem tartozik fokonyhoz'],
['Revenue Lineitem':'Revenue Lineitem':'ARPU'],
STET
);

FEEDERS;
# E/1: eszközértékesítés
#Tényidőszakban, tervben
#darabszám: connect, input
#fajlagos(ARPU) kalkulált, input
['ledger':'ledger':{'91401'},'Revenue Lineitem':'Revenue Lineitem':{'Efi szam','Bevetel'},'Version':'Forecast',
 'Product':'Nem tartozik termekhez', 'Revenue Measure':'Revenue Measure':'Value'] =>
['Revenue Lineitem':'Revenue Lineitem':'ARPU'];

#TITLE: 
#VIEWS:
#LINKED:
#bevétel: DWH(főkönyv), kalkuláció
['ledger':'ledger':{'91401'},'Revenue Lineitem':'Revenue Lineitem':{'Efi szam','ARPU'},'Version':'Forecast',
 'Product':'Nem tartozik termekhez', 'Revenue Measure':'Revenue Measure':'Value'] =>
['Revenue Lineitem':'Revenue Lineitem':'Bevetel'];

#TITLE: 
#VIEWS:
#LINKED:
#költség%: kalkulált, input
['ledger':'ledger':{'91401'},'Revenue Lineitem':'Revenue Lineitem':{'Bevetel','Koltseg'},'Version':'Forecast',
 'Product':'Nem tartozik termekhez', 'Revenue Measure':'Revenue Measure':'Value'] =>
['Revenue Lineitem':'Revenue Lineitem':'Koltseg Rata'];

#TITLE: 
#VIEWS:
#LINKED:
#költség: DWH(főkönyv), kalkuláció
['ledger':'ledger':{'91401'},'Revenue Lineitem':'Revenue Lineitem':{'Bevetel','Koltseg Rata'}, 'Version':'Forecast',
 'Product':'Nem tartozik termekhez', 'Revenue Measure':'Revenue Measure':'Value'] =>
['Revenue Lineitem':'Revenue Lineitem':'Koltseg'];

#TITLE: 
#VIEWS:
#LINKED:
# E/2:modem szabályok
# darab (TM1) tény és terv időszakba is a naturáliából
# ARPU: tervben input, tény időszakban kalkuláció (főkönyv (DWH) / darab (TM1) )
['ledger':'ledger':{'91502'},'Revenue Lineitem':'Revenue Lineitem':{'Bevetel','Efi szam'},'Product':'Nem tartozik termekhez', 'Version':'Forecast',
'Revenue Measure':'Revenue Measure':'Value'] =>
['Revenue Lineitem':'Revenue Lineitem':'ARPU'];

#TITLE: 
#VIEWS:
#LINKED:
# bevétel: ténynél DWH, tervnél:ARPU X darab
['ledger':'ledger':{'91502'},'Revenue Lineitem':'Revenue Lineitem':{'Efi szam','ARPU'},'Product':'Nem tartozik termekhez','Version':'Forecast',
'Revenue Measure':'Revenue Measure':'Value'] =>
['Revenue Lineitem':'Revenue Lineitem':'Bevetel'];

#TITLE: 
#VIEWS:
#LINKED:
# E/3: dekóder STB
# ARPU: tervben input, tény időszakban kalkuláció (főkönyv (DWH) / darab (TM1) )
['ledger':'ledger':{'91501'},'Revenue Lineitem':'Revenue Lineitem':{'Efi szam','Bevetel'},'Version':'Forecast',
'Revenue Measure':'Revenue Measure':'Value'] =>
['Revenue Lineitem':'Revenue Lineitem':'ARPU'];

#TITLE: 
#VIEWS:
#LINKED:
# bevétel: ténynél DWH, tervnél:ARPU X darab
['ledger':'ledger':{'91502'},'Revenue Lineitem':'Revenue Lineitem':{'Efi szam','ARPU'},'Version':'Forecast',
'Revenue Measure':'Revenue Measure':'Value'] =>
['Revenue Lineitem':'Revenue Lineitem':'Bevetel'];

#TITLE: 
#VIEWS:
#LINKED:
#### általánosan
#'ledger':'ledger':{'91101','91201','91202','91301','91302','91303','91401'},

['Revenue Lineitem':'Revenue Lineitem':{'Efi szam','ARPU'}, 'Revenue Measure':'Revenue Measure':'Value','Version':'Forecast'] =>
['Revenue Lineitem':'Revenue Lineitem':'Bevetel'];


#Botond 04.03
#NMHH Other Cost kalkuláció
[ 'ledger':'ledger':{'91101','91102','91201','91202','91301','91302','91303','91304'},
'Product':'Total Products','Version':'Forecast','Revenue Lineitem':'Revenue Lineitem':{'Bevetel'},'Revenue Measure':'Revenue Measure':'Value'] =>
DB('Other Cost', !Version, !Period, '53301', 'Cost', 'Value');

#Botond 04.11
#81401 Eladott áruk beszerzési értéke 
['ledger':'ledger':{'91401'},'Revenue Lineitem':'Revenue Lineitem':'Koltseg',
 'Product':'Nem tartozik termekhez', 'Revenue Measure':'Revenue Measure':'Value'] =>
 DB('Direct Cost', !Version, !Period, '81401', 'Cost', 'Value');



# Direkt Cost Feeder
['ledger':'ledger':{'91401'},'Revenue Lineitem':'Revenue Lineitem':'Koltseg','Version':'Forecast',
'Revenue Measure':'Revenue Measure':'Value'] =>
DB('Direct Cost', !Version, !Period, '81401', 'Cost', 'Value');



# Mody 05 14
# Erkim FEEDER
['Version':'Forecast','Ledger':'Ledger':'Revenue','Product':'Total Products','Revenue Lineitem':'Revenue Lineitem':'Bevetel','Revenue Measure':'Revenue Measure':'Value'] => 
DB('Erkim', !Version, !Period, !ledger, 'Value');





