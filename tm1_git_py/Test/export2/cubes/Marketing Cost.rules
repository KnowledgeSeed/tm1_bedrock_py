# Módos Dávid 2024 08 31

SKIPCHECK;

# C-s szabályok
# Éves miatt
['Marketing Cost':'Marketing Cost':'Bevaltasi arany','Marketing Cost Measure':'Value'] = C:
IF( ELLEV('Period',!Period)>0,
  DB('Marketing Cost', !Version, !Period | '12', !Marketing Cost, !Marketing Cost Measure),
Continue);

['Marketing Cost':'Marketing Cost':'Gift point average cost','Marketing Cost Measure':'Value'] = C:
IF( ELLEV('Period',!Period)>0,
  DB('Marketing Cost', !Version, !Period | '12', !Marketing Cost, !Marketing Cost Measure),
Continue);

['Marketing Cost':'Marketing Cost':'Gift point average logistics cost','Marketing Cost Measure':'Value'] = C:
IF( ELLEV('Period',!Period)>0,
  DB('Marketing Cost', !Version, !Period | '12', !Marketing Cost, !Marketing Cost Measure),
Continue);

# verzo szabályok
[]= N:
IF(ELISPAR('Version','Report',!Version) = 1,
STET,Continue);

# tény szabályok behúzása és kalkulációk
# Bevaltasi arany

['Marketing Cost':'Marketing Cost':'Bevaltasi arany','Marketing Cost Measure':'Value'] =N:
IF( DIMIX('Period',!Period) < DIMIX('Period',DB('zSYS Maintenance Parameter','ActualMonth','S Type')),
['Marketing Cost':'Marketing Cost':'Osszes bevaltott pontok teny','Marketing Cost Measure':'Value']
\
['Marketing Cost':'Marketing Cost':'Total Gift Point','Marketing Cost Measure':'Value']
*
100,
STET
);

# Szumma bevaltott ajandek pont = Bevaltasi arany * Total Gift Point
['Marketing Cost':'Marketing Cost':'Szumma bevaltott ajandek pont','Marketing Cost Measure':'Value'] =C:
  ['Marketing Cost':'Marketing Cost':'Total Gift Point'] *
  ['Marketing Cost':'Marketing Cost':'Bevaltasi arany'] / 100;


# gift point average cost
['Marketing Cost':'Marketing Cost':'Gift point average cost','Marketing Cost Measure':'Value'] =N:
IF( DIMIX('Period',!Period) < DIMIX('Period',DB('zSYS Maintenance Parameter','ActualMonth','S Type')),
['Marketing Cost':'Marketing Cost':'Gift Cost Fact','Marketing Cost Measure':'Value']
\
['Marketing Cost':'Marketing Cost':'Osszes bevaltott pontok teny','Marketing Cost Measure':'Value'],
STET
);

# Gift point average logistics cost
['Marketing Cost':'Marketing Cost':'Gift point average logistics cost','Marketing Cost Measure':'Value'] =N:
IF( DIMIX('Period',!Period) < DIMIX('Period',DB('zSYS Maintenance Parameter','ActualMonth','S Type')),

['Marketing Cost':'Marketing Cost':'Logistics Cost Fact','Marketing Cost Measure':'Value']
\
['Marketing Cost':'Marketing Cost':'Osszes bevaltott pontok teny','Marketing Cost Measure':'Value']
,
STET
);


# Gift Cost = Gift point average cost * Szumma bevaltott ajandek pont
['Marketing Cost':'Marketing Cost':'Gift Cost','Marketing Cost Measure':'Value'] =C:
['Marketing Cost':'Marketing Cost':'Szumma bevaltott ajandek pont']
*
['Marketing Cost':'Marketing Cost':'Gift point average cost'];



# Logistics Cost = Gift point average logistics cost * Szumma bevaltott ajandek pont
['Marketing Cost':'Marketing Cost':'Logistics Cost','Marketing Cost Measure':'Value'] =C:
['Marketing Cost':'Marketing Cost':'Szumma bevaltott ajandek pont']
*
['Marketing Cost':'Marketing Cost':'Gift point average logistics cost'];

# Marketing és ajándék költségek
['Marketing Cost':'Marketing Cost':'55501','Marketing Cost Measure':'Value'] =C:
IF( DIMIX('Period',!Period) < DIMIX('Period',DB('zSYS Maintenance Parameter','ActualMonth','S Type')),
['Marketing Cost':'Marketing Cost':'55501 Fact','Marketing Cost Measure':'Value']
,
['Marketing Cost':'Marketing Cost':'Logistics Cost','Marketing Cost Measure':'Value']
+
['Marketing Cost':'Marketing Cost':'Gift Cost','Marketing Cost Measure':'Value']
);



FEEDERS;
# arany feeder
['Marketing Cost':'Marketing Cost':{'Osszes kiosztott pont','Osszes bevaltott pontok teny'},'Marketing Cost Measure':'Value'] =>
['Marketing Cost':'Marketing Cost':'Bevaltasi arany'];


['Marketing Cost':'Marketing Cost':{'Total Gift Point','Bevaltasi arany'},'Marketing Cost Measure':'Value'] =>
['Marketing Cost':'Marketing Cost':'Szumma bevaltott ajandek pont'];


['Marketing Cost':'Marketing Cost':{'Szumma bevaltott ajandek pont','Gift point average cost'},'Marketing Cost Measure':'Value'] =>
['Marketing Cost':'Marketing Cost':'Gift Cost'];

['Marketing Cost':'Marketing Cost':{'Szumma bevaltott ajandek pont','Gift point average logistics cost'},'Marketing Cost Measure':'Value'] =>
['Marketing Cost':'Marketing Cost':'Logistics Cost'];

['Marketing Cost':'Marketing Cost':{'55501 Fact'},'Marketing Cost Measure':'Value'] =>
['Marketing Cost':'Marketing Cost':'55501'];






