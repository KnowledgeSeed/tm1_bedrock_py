#region Prolog

#****Begin: Generated Statements***
#****End: Generated Statements****


# =========================================================
# Original Date Created              2016-12-13
# Originator                                 Jozsef Buzas
# Purpose                                   Archive a file (Creates a ZIP in a separate folder)
# Change Date                           
# Change Request Reference    
# Change Made by                     
# =========================================================

# Central logging - start
nRandomNumber = Round ( RAND * 1000000);
sRandomNumber = NumberToString(nRandomNumber);
pStartTime=NumberToString(NOW());
pProcessName =  GetProcessName();

pParams= 'pParamList|' | pParamList | '|pDebug|' | NumberToString(pDebug);

ExecuteProcess ('zSYS Process Log Create Entry','pProcessName',pProcessName,'pStartTime',pStartTime,'pParams',pParams,'pRandomNumber',sRandomNumber,'pCallerID',pCallerID);
#OwnCallerID
sCallerID = pStartTime |' - '| pProcessName|'-'|sRandomNumber;

### DEFINE VARIABLES

## General Variables
vsSysControl = 'zSYS Maintenance Parameter';
vsDimensionExportUNC = CellGetS(vsSysCOntrol, 'Dimension Export UNC','S type');
vsProcessDebuggingUNC = CellGetS(vsSysCOntrol,'Process Debugging UNC' ,'S type');
vsLogDirectoryPath = CellGetS(vsSysCOntrol,'Log Directory Path' ,'Stype');
vsTimeStamp = TIMST(NOW(),'\Y\m\d\h\i\s');
vsDebugFile = vsProcessDebuggingUNC | pProcessName | ' ' | vsTimeStamp | '.txt';
vsLogFile = vsLogDirectoryPath | pProcessName | ' ' | vsTimeStamp | '.txt';
sValueDelimiter =  CellGetS(vsSysControl, 'DelimiterBetweenParamAndValue','S type');
sParamDelimiter =  CellGetS(vsSysControl, 'DelimiterBetweenParameters','S type');
i = 1;

WHILE (pParamList @<> '')  ;
   nPos = SCAN(sValueDelimiter, pParamList);
   sParDimName = SUBST(pParamList, 1, nPos-1);
   pParamList = SUBST(pParamList, nPos+1, LONG(pParamList) - nPos);

   nPos = SCAN(sParamDelimiter, pParamList);
   nPos = If(nPos = 0, LONG(pParamList) +1, nPos);
   sParElName = SUBST(pParamList, 1, nPos-1);
   pParamList = SUBST(pParamList, nPos+1, LONG(pParamList) - nPos);

IF(i = 1);
    sDirectoryParam = sParElName;
ELSEIF (i = 2);
    sTargetFile = sParElName;
ELSE;
    sFilePathParam = sParElName;
ENDIF;
   i = i + 1;
END;

## Local Variables
vsWinZip = CellGetS(vsSysCOntrol, 'WinZip','S type');
vsCSVSourceDirectory =  CellGetS(vsSysCOntrol, sFilePathParam,'S type');
vsCSVArchiveDirectory = CellGetS(vsSysCOntrol, sDirectoryParam,'S type');
cFileName = vsCSVSourceDirectory | sTargetFile;


#####  Move and ZIP the old files to Archive folder
vsTimeStamp= TIMST(NOW,'\Y-\m-\d-\h-\i-\s');

vsNewFile = Subst(sTargetFile,1,Long(sTargetFile)-4) | ' ' | vsTimeStamp |'.zip';

vsCMD = vsWinZip | ' "' | vsCSVArchiveDirectory | vsNewFile | '" "' | cFileName | '"' ;

IF (FileExists(cFileName) =1);
    IF( pDebug <> 0);
        ASCIIOUTPUT(vsDebugFile,vsCMD);
    ELSE;
        ExecuteCommand(vsCMD , 1);
    ENDIF;
EndIF;

LogOutput('INFO', 'File Archivation is done: '| vsNewFile);
#endregion
#region Metadata

#****Begin: Generated Statements***
#****End: Generated Statements****
#endregion
#region Data

#****Begin: Generated Statements***
#****End: Generated Statements****
#endregion
#region Epilog

#****Begin: Generated Statements***
#****End: Generated Statements****



# =============  Process logging

####General LOGGING frame###
ExecuteProcess('zSYS Process Log Close Entry','pProcessName',pProcessName,'pStartTime',pStartTime,'pRandomNumber',sRandomNumber,'pProcessErrorLogFile',GetProcessErrorFilename);
####General LOGGING frame###
#endregion
