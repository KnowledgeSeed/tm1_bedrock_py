#region Prolog

#****Begin: Generated Statements***
#****End: Generated Statements****


# =========================================================
# Original Date Created              2015-06-04
# Originator                                 Zsolt Moravcsik
# Purpose                                   Maintain All N Element DimName C level knot in busienss dimension only
# =========================================================

# Central logging - start
nRandomNumber = Round ( RAND * 1000000);
sRandomNumber = NumberToString(nRandomNumber);
pStartTime=NumberToString(NOW());
pProcessName =  GetProcessName();
pParams= 'pDimname|'|pDimName|'|pDebug|'|Numbertostring(pDebug);
#ExecuteProcess ('zSYS Process Log Create Entry','pProcessName',pProcessName,'pStartTime',pStartTime,'pParams',pParams,'pRandomNumber',sRandomNumber,'pCallerID',pCallerID);
#OwnCallerID
sCallerID = pStartTime |' - '| pProcessName|'-'|sRandomNumber;

# Define variables
sSysControl = 'zSYS Maintenance Parameter';
sDimensionExportUNC = CellGetS(sSysCOntrol, 'Dimension Export UNC','Stype');
sProcessDebuggingUNC = CellGetS(sSysCOntrol,'Process Debugging UNC' ,'Stype');
sLogDirectoryPath = CellGetS(sSysCOntrol,'Log Directory Path' ,'Stype');
sProcessName =  GetProcessName();
sTimeStamp = TIMST(NOW(),'\Y\m\d\h\i\s');
sDebugFile = sProcessDebuggingUNC | sProcessName | ' ' | sTimeStamp | '.txt';
sLogFile = sLogDirectoryPath | sProcessName | ' ' | sTimeStamp | '.txt';

sSubname = 'All N Level Element';
sSubNameRand = sProcessName | sRandomNumber;

# Check Valid execution
IF(DimIX(pDimName,'All '|pDimname) = 0);
   ProcessBreak();
EndIF;

# Create random N levele element subset and create normal one if not exist

sStringParam = '{TM1FILTERBYLEVEL( {TM1SUBSETALL( [' | pDimName | '] )}, 0)}';
  ExecuteProcess('zSYS Dimension Subset Create','pDimName',pDimname,'pSubName',sSubNameRand,'pStringParam',sStringParam,'pExpandAndChangeCtoN',0,'pCallerID',sCallerID);

IF(SubsetExists(pDimname, sSubname) = 0);
   ExecuteProcess('zSYS Dimension Subset Create','pDimName',pDimname,'pSubName',sSubName,'pStringParam',sStringParam,'pExpandAndChangeCtoN',0,'pCallerID',sCallerID);
EndIf;

#Check if the content of the 2 subset is the same if not give an error message into Message log
IF(SubsetGetSize( pDimName, sSubName ) <> SubsetGetSize( pDimName, sSubNameRand )  );
   ItemReject('The All N Level Element subset of ' | pDimName | ' is not uptodate, please check it. Subset Size All N element:' | NumberTostring(SubsetGetSize( pDimName, sSubName ) ) | ' Rnad subset Size: ' | NumberTostring(SubsetGetSize( pDimName, sSubNameRand ) ));
EndIF;

# Check All N Element | pDimname C element exist , if not create it under All pDimanme element
sAllNElement = 'All N Element '|pDimname;

If (ElIsAnc(pDimName,'All ' |pDimName, sAllNElement)=0);
 DimensionElementComponentAdd( pDimName, 'All ' | pDimName, sAllNElement, 1 );
EndIf;

#Update the All N Level pDimname Element
#If it exist first clean it
i = 1;
iMax = ElComPN(pDimName, sAllNElement);
WHILE(i<=iMax);

  DimensionElementComponentDelete( pDimName, sAllNElement, ElComp( pDimName, sAllNElement, 1 ) );

  i = i+1;
END;

#Recreate the new C level knot
i = 1;
iMax = SubsetGetSize( pDimName, sSubNameRand );

WHILE(i<=iMax);
    sElement = SubsetGetElementName( pDimName, sSubNameRand,i );
  DimensionElementComponentAdd( pDimName, sAllNElement, sElement,1 );

  i = i+1;
END;

#Update All p Dimname child weight woth 0 expect All N element 
#If it exist first clean it
i = 1;
sAllElement=  'All '|pDimName;
iMax = ElComPN(pDimName, sAllElement);
WHILE(i<=iMax);
  sElement =  ElComp( pDimName, sAllElement, 1 );
  DimensionElementComponentDelete( pDimName, sAllElement, sElement );
  if(sElement @= sAllNElement);
     nWeight = 1;
  Else;
    nWeight = 0;
  EndIF;
  DimensionElementComponentAdd( pDimName, sAllElement, sElement, nWeight  );

  i = i+1;
END;

If(Dimix('}ElementAttributes_'|pDimName,'Code + Name') > 0);
SubsetAliasSet( pDimName, 'All N Level Element', 'Code + Name' );
Else;
SubsetAliasSet( pDimName, 'All N Level Element', 'Caption_Default' );
EndIf;
#endregion
#region Metadata

#****Begin: Generated Statements***
#****End: Generated Statements****
#endregion
#region Data

#****Begin: Generated Statements***
#****End: Generated Statements****
#endregion
#region Epilog

#****Begin: Generated Statements***
#****End: Generated Statements****

#CleanUp
SubsetDestroy(pDimName, sSubnameRand);

# Central logging - finish
#ExecuteProcess('zSYS Process Log Close Entry','pProcessName',pProcessName,'pStartTime',pStartTime,'pRandomNumber',sRandomNumber,'pProcessErrorLogFile',GetProcessErrorFilename);

#endregion
